class Player {
    // Constants
    static int x_width;                 // Bounding box width
    static int y_height;                // Bounding box height
    static int x1_mem;                  // (Initial) x1 position in memory
    static int x1;                      // x1 position on screen
    static int x2;                      // x2 position on screen
    static int y1_init;                 // Initial y1 position on screen
    static int y1_init_mem;             // Initial y1 position in memory
    static int y_vel;                   // Movement speed on screen
    static int y_vel_mem;               // Movement speed in memory
    static int y_screen_lower_bound;    // Lower bound of y1 position on screen
    static int y_screen_upper_bound;    // Upper bound of y1 position on screen

    // Fields
    field int y1;                       // y1 position on screen
    field int mem_pos;                  // Position in memory
    field int skin;                     // Animation frame
    field int tick;                     // Animation tick
    field boolean value;                // Player's value

    function void init(int y_upper_part) {
        let x_width = 12;
        let y_height = 32;
        let x1_mem = 4;
        let y_vel = 3;

        let x1 = x1_mem * 16;
        let x2 = x1 + x_width;
        let y1_init = y_upper_part + (256 - y_upper_part - y_height) / 2;
        let y1_init_mem = y1_init * 32;
        let y_vel_mem = y_vel * 32;
        let y_screen_lower_bound = y_upper_part + y_vel;
        let y_screen_upper_bound = 255 - y_height - y_vel;
        return;
    }

    constructor Player new() {
        do reset();
        return this;
    }

    method void reset() {
        let y1 = y1_init;
        let mem_pos = x1_mem + y1_init_mem;
        let skin = 0;
        let tick = 0;
        let value = false;
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void move(boolean up) {
        if (up) {
            if (y1 > y_screen_lower_bound) {
                let y1 = y1 - y_vel;
                let mem_pos = mem_pos - y_vel_mem;
            }
        } else {
            if (y1 < y_screen_upper_bound) {
                let y1 = y1 + y_vel;
                let mem_pos = mem_pos + y_vel_mem;
            }
        }
        return;
    }

    method void setVal(boolean val) {
        let value = val;
        return;
    }

    method void tick() {
        return;
    }

    method void render() {
        do SpriteManager.draw_player(mem_pos);
        return;
    }

    method void update() {
        do tick();
        do render();
        return;
    }

    method int x1() { return x1; }
    method int y1() { return y1; }
    method int x2() { return x2; }
    method int y2() { return y1 + y_height; }
}