class Game {
    // Constants
    static int delay;
    // Objects
    field Player player;
    field Score score;
    field Obstacle obs1;
    field Obstacle obs2;
    // Internal states
    field char key;
    field char last_key;
    field boolean first_start;
    field boolean start;
    field boolean pause;
    // Random number seed
    field int seed;

    function void init() {
        let delay = 5;
        return;
    }

    constructor Game new() {
        do Player.init();
        do Score.init();
        do Obstacle.init();
        let player = Player.new();
        let score = Score.new();
        let obs1 = Obstacle.new(511);
        let obs2 = Obstacle.new(511 + Obstacle.interval());
        let first_start = false;
        let start = false;
        let seed = 0;
        let last_key = 0;
        return this;
    }

    method void dispose() {
        do player.dispose();
        do score.dispose();
        do obs1.dispose();
        do obs2.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method boolean whitespace() {
        let key = Keyboard.keyPressed();
        if (key = 32 & ~(last_key = 32)) {
            let last_key = key;
            return true;
        }
        let last_key = key;
        return false;
    }

    method void handle_input() {
        let key = Keyboard.keyPressed();
        if ((key = 32) & ~(last_key = 32)) { // whitespace
            let pause = ~pause;
        } else {
            if (~pause) {
                if (key = 131) { // up arrow
                    do player.move(true);
                } else {
                    if (key = 133) { // down arrow
                        do player.move(false);
                    }
                }
            }
        }
        let last_key = key;
        return;
    }

    method void update() {
        do player.update();
        do score.show();
        do obs1.update();
        do obs2.update();
        if (obs1.collision(player.x1(), player.y1(), player.x2(), player.y2()) | obs2.collision(player.x1(), player.y1(), player.x2(), player.y2())) {
            let start = false;
        }
        return;
    }

    method boolean run() {
        // first start
        while(~first_start) {
            let seed = seed + 1;
            if (seed < 0) {
                let seed = 0;
            }
            if (whitespace()) {
                do LCGRandom.setSeed(seed);
                let first_start = true;
                let start = true;
            }
        }
        // During the game
        while (start) {
            do handle_input();
            if (~pause) {
                do update();
            }
            do Sys.wait(delay);
        }
        // Game over, restart or not
        while (~start) {
            if (whitespace()) {
                do reset();
            }
        }
        return true;
    }

    method void reset() {
        let start = true;
        let pause = false;
        do Screen.clearScreen();
        do player.reset();
        do score.reset();
        do obs1.reset(511);
        do obs2.reset(511 + Obstacle.interval());
        return;
    }
}