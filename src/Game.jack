class Game {
    // Constants
    static int word_width;
    static int delay_init;
    static int score_per_level;
    static int y_upper_margin;

    // Objects
    field Score score;
    field Level level;
    field Player player;
    field Obstacle obs1;
    field Obstacle obs2;

    // Internal states
    field int delay;
    field char key;
    field char last_key;
    field boolean first_start;
    field boolean start;
    field boolean pause;
    field int obs_collision_state;

    // Player data
    field int player_x1;        // constant
    field int player_y1;
    field int player_x2;        // constant
    field int player_y2;
    field boolean player_value;

    // Random number seed
    field int seed;

    function void init() {
        let word_width = 1;
        let delay_init = 4;
        let score_per_level = 10;
        let y_upper_margin = 42;
        return;
    }

    constructor Game new() {
        do Score.init(word_width);
        do Level.init(word_width);
        do Player.init(y_upper_margin);
        do Obstacle.init(y_upper_margin);

        let score = Score.new();
        let level = Level.new();
        let player = Player.new();
        // Player data
        let player_x1 = player.x1();
        let player_y1 = player.y1();
        let player_x2 = player.x2();
        let player_y2 = player.y2();
        let player_value = false;

        let obs1 = Obstacle.new(512);
        let obs2 = Obstacle.new(512 + 256);

        let delay = delay_init;
        let key = 0;
        let last_key = 0;
        let first_start = false;
        let start = false;
        let pause = false;
        let obs_collision_state = 0;
        let seed = 0;
        return this;
    }

    method void dispose() {
        do score.dispose();
        do player.dispose();
        do obs1.dispose();
        do obs2.dispose();

        do Memory.deAlloc(this);
        return;
    }

    method boolean whitespace() {
        let key = Keyboard.keyPressed();
        if (key = 32 & ~(last_key = 32)) {
            let last_key = key;
            return true;
        }
        let last_key = key;
        return false;
    }

    method void handle_input() {
        let key = Keyboard.keyPressed();
        if ((key = 32) & ~(last_key = 32)) { // whitespace
            let pause = ~pause;
        } else {
            if (~pause) {
                if (key = 131) { // up arrow
                    do player.move(true);
                } else {
                    if (key = 133) { // down arrow
                        do player.move(false);
                    }
                }
            }
        }
        let last_key = key;
        return;
    }

    method void draw_upper() {
        do Screen.setColor(true);
        do Screen.drawRectangle(0, y_upper_margin - 2, 511, y_upper_margin - 1);
        do Score.printSCORE();
        do score.show();
        do Level.printLEVEL();
        do level.show();
        return;
    }

    method void draw_gameover() {
        var int mem_offset;
        do Screen.setColor(true);
        do Screen.drawRectangle(11*16, 108, 21*16, 108 + 40);
        do Screen.setColor(false);
        do Screen.drawRectangle(11*16 + 4, 108 + 4, 21*16 - 4, 108 + 40 - 4);

        let mem_offset = 12 + (117 * 32);
        do DrawWord.draw_g(mem_offset);
        do DrawWord.draw_a(word_width + mem_offset);
        do DrawWord.draw_m(word_width * 2 + mem_offset);
        do DrawWord.draw_e(word_width * 3 + mem_offset);
        do DrawWord.draw_o(word_width * 4 + mem_offset);
        do DrawWord.draw_v(word_width * 5 + mem_offset);
        do DrawWord.draw_e(word_width * 6 + mem_offset);
        do DrawWord.draw_r(word_width * 7 + mem_offset);

        do Output.moveCursor(14,21);
        do Output.printString("Press SPACE to restart!");
        return;
    }

    method void update() {
        do player.update();
        let player_y1 = player.y1();
        let player_y2 = player.y2();

        do obs1.update();
        do obs2.update();

        let obs_collision_state = 
        obs1.collision(player_x1, player_y1, player_x2, player_y2, player_value) |
        obs2.collision(player_x1, player_y1, player_x2, player_y2, player_value);
        if (obs_collision_state = -1) {
            let start = false;
        } else {if (obs_collision_state = 1) {
            do player.setVal(Gate.output());
            if (~player_value & player.value()) {
                do score.increment();
                // Level up
                if(~(delay = 0) & (LCGRandom.mod(score.getScore(), score_per_level) = 0)) {
                    let delay = delay - 1;
                    do level.increment();
                    do Obstacle.increNull();
                }
            }
            let player_value = player.value();
        }}
        return;
    }

    method boolean run() {
        // first start
        while(~first_start) {
            let seed = seed + 1;
            if (seed < 0) {
                let seed = 0;
            }
            if (whitespace()) {
                do LCGRandom.setSeed(seed);
                let first_start = true;
                let start = true;
                do draw_upper();
                // LCGRandom seed set, set random values
                do obs1.reset();
                do obs2.reset();
            }
        }
        // During the game
        while (start) {
            do handle_input();
            if (~pause) {
                do update();
            }
            do Sys.wait(delay);
        }
        // Game over, restart or not
        do draw_gameover();
        while (~start) {
            if (whitespace()) {
                do reset();
            }
        }
        return true;
    }

    method void reset() {
        let delay = delay_init;    
        let start = true;
        let pause = false;
        do Screen.clearScreen();

        do score.reset();
        do level.reset();
        do player.reset();
        do obs1.reset();
        do obs2.reset();

        do draw_upper();

        return;
    }
}